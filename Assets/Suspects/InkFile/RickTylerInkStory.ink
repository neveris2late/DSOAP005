VAR q1_selected = true
VAR q2_selected = true
VAR q3_selected = false
VAR q4_selected = false
VAR q5_selected = true

VAR questions_asked = 0
我：你好，泰勒先生
泰勒：你是？
->open_1
=== open_1 ===
*[出示证件]
我：我是特别调查处的温泽零
泰勒：你好，温警官，需要我配合什么工作嘛？
-> open_2

=== open_2 ===
*[出示调查令]
我：根据川流城反情报工作特别条令第17条
我：需要你配合我回答一些问题
我：不会耽误你太多的时间，泰勒先生
泰勒：我有的选吗，警官？
->open_3

=== open_3 ===
*[没有]
我：没有
泰勒：那问吧，警官
泰勒：我一定知无不答
-> question_hub

=== question_hub ===
* {q1_selected}[日夜风的常客]
    ~ questions_asked ++
    -> branch_A
* {q2_selected}[海鲜餐馆的龙虾]
    ~ questions_asked++
    -> branch_B
* {q3_selected}[女性朋友的仿生狗]
    ~ questions_asked++
    -> branch_C
* {q4_selected}[如何处置脱逃的仿生人]
    ~ questions_asked++
    -> branch_D
* {q5_selected}[关于卡斯托里尔·灯]
    ~ questions_asked++
    -> branch_E
    
=== branch_A ===
我：我们的调查员发现
我：你位于曲微生区的日夜风咖啡酒馆
我：有数位常客是来自于第五新联合的仿生人间谍
我：我们相信你对此事并非一无所知吧？
泰勒：警官，我虽然确实热衷于对我的门店进行精细化管理
泰勒：但再如何细致的管理者
泰勒：也无法认识自己的每一位顾客
泰勒：不是吗？
*[出示数据]
我：在这几位间谍相聚的三次活动中
我：前两次都正好遇上你在这家店里
我：这不会只是巧合吧？
泰勒：我相信你们不会把这种幼稚的推理
泰勒：作为证据呈现给区检看的
泰勒：对吧，警官？
-> branch_A.friend

*[施加压力，质疑关联]
我：跟你客气你别给我在这打太极
-> branch_A.friend

= friend
*[询问安德兰的情况]
我：说说你和安德兰是怎么认识的？
泰勒：安德兰是？
-> friend_A
= friend_A
*[平静陈述]
我：泰瑞斯·安德兰
我：日夜风咖啡酒曲微生分店的夜场领班
我：还在你这儿做过不少美形义体改造吧？
我：你不认识啦？
泰勒：BigT，是的是的，他是我钦点的夜场领班
泰勒：我跟他是在冈特雷斯酒吧认识的
泰勒：他那时候还在那当调酒师呢
泰勒：我那天跟一位朋友去喝酒
泰勒：他调的几杯酒实在是非常惊艳
泰勒：后来我们交流也很愉快
泰勒：我就给他挖过来了
-> friend_B
*[挑衅]
我：你自己钦点的夜场领班
我：你在这装不认识
我：你他妈的是当我们SWG警员傻逼是吧？
泰勒：抱歉抱歉温警官，年龄大了，忘性大
泰勒：我们都叫他BigT
泰勒：你说安德兰一下子没想起来
-> friend_B

= friend_B
*[安德兰的私下交易]
我：他与那几位仿生人间谍有私下里的金钱交易
我：你清楚吗？
我：在必要的时候
我：他会为他们关闭店内的监控与录音设备
泰勒：还有这种事？
泰勒：警官，这我真不知道啊
泰勒：我虽然时不时会去店里
泰勒：店里的设备和管理都是领班和经理负责
泰勒：我最多是过问一下
泰勒：不可能事无巨细地了解啊
->check_progress

->check_progress

=== branch_B ===
我：设想一下，你正在看一部动物保护革命前的电影
我：电影中的几个角色走入渔人港街区
我：他们逛了一圈后走入一家海鲜餐馆
我：其中一位角色点了龙虾
我：大厨来到餐桌边把龙虾放入烧开的火锅之中
我：你有何感受？
泰勒：我不得不说，警官
泰勒：你是知道怎么让我起鸡皮疙瘩的
泰勒：我真的难以想象过去的人会这样对待活的动物
泰勒：真是疯狂的年代啊

*[你的店里销售过活物吗？]
我：你的店里没有销售过活物制品吗？
泰勒：我的天，警官，你把我当成什么了？
泰勒：销售活物制品，别说钱不钱的事了
泰勒：什么样的客人敢吃那种东西
->check_progress

*[询问他的宠物]
我：听说你家有一窝宠物连城白鸭？
泰勒：是啊，它们可是我的宝贝
泰勒：鸭妈妈旺达是我从朋友手上买来的
泰勒：后来好不容易给她配到了种
泰勒：但是这一窝鸭蛋孵化率也是很低
泰勒：现在的三只小鸭也都是专门请了生物学家在照料
->check_progress

=== branch_C ===
我：假如一位你爱慕的女性邀请你来到她的公寓
我：她的仿生小狗突然出现了故障
我：向你扑过来——
泰勒：我还真遇到过这样的事
泰勒：有一次我去到朋友的家里
泰勒：她的仿生鹰可能把我识别成了猎物
泰勒：一下子朝我俯冲过来
泰勒：把我手臂啄了好几个洞

*[继续问题]
->continue_question

*[询问为何会被啄伤]
我：你的反应芯片不起作用吗？
泰勒：警官，我当然能够反应过来
泰勒：只是我不想伤害朋友的电子宠物
泰勒：而且毕竟手臂换一下也花不了几个钱
泰勒：以和为贵，不是吗，警官？
->continue_question

= continue_question
我：仿生小狗朝你扑来后
我：绝望地看着你
我：你发现它的身上有许多被鞭打的伤痕
我：你会怎么做？
泰勒：我毕竟不是什么圣人，是吧？
泰勒：或许我会试试跟她聊聊她如何看待她的仿生宠物
泰勒：但对别人的生活指手画脚我大概是不会去做的
泰勒：何况是在这种暧昧的场合下，警官(读数40）
->check_progress

=== branch_D ===
我：假设你在街边
我：看到一个瘫坐在人行道边的人
我：走近发现他是一个从某家仿生人流水线工厂脱逃的仿生人
我：他向你请求不要报警把他抓捕归案
我：你会怎么做？
泰勒：警官，你对如今的政治局势也全部了解
泰勒：这样的问题我该如何回答呢？
泰勒：自良心考虑还是自立场考虑？（读数30）

*[施加压力]
我：你别他妈的跟我拐弯抹角
我：怎么想的，就怎么说
泰勒：是，是，警官
泰勒：实话说我理都不会理他
泰勒：对我来说
泰勒：我最忌讳这种容易惹得一身骚的意外了
泰勒：何况是个仿生人奴隶
泰勒：我不会报警抓他，也不会救他
泰勒：为这种事耽误时间，太不值当了（读数40）
->check_progress

*[表示理解]
我：我当然了解
我：今天我们不是来谈政治的
我：你实话实说就行
泰勒：跟你谈话还是很愉快的，温警官
泰勒：对于仿生人，我一贯的态度就是他们应当满足于自己的劳动者身份
泰勒：脱逃出来，难道还能逃出川流城不成？
泰勒：我肯定会报警给他送去局里（读数50）
->check_progress

=== branch_E ===
我：你曾经为卡斯托里尔·灯进行了
我：右锁骨电子纹身与可发光肌肤纹理改造手术，对吧？
泰勒：确实如此，你们调查的还真清楚
*[询问是否知晓灯的间谍活动]
泰勒：警官，这可不能乱说啊！！
泰勒：我虽然帮他做了义体塑形
泰勒：但我可不清楚其他事啊（读数80）
-> branch_E_1

= branch_E_1
*[询问更多信息]
我：这位灯先生和你在塑形业务之外好像也有往来吧？
泰勒：我们确实有过一些私下的聚会
泰勒：但是警官，我可和什么仿生人组织无关啊
泰勒：我从来没参与过任何与仿生人组织有关的事务啊
-> branch_E_2

*[高压逼问他为何隐瞒情报]
我：你是不是太不把我们SWG当回事了？
我：该说的不说，下次来的就不光是我了
泰勒：哎哟，哎哟，我说我说
泰勒：我们私下里真的没有聊过仿生人组织的事
泰勒：他是我的男模之一，有时候我会聘请他去服务一下重要客户
泰勒：所以偶尔带他去参加一下重要的聚会
-> branch_E_3

= branch_E_2
*[私下聊什么？]
我：你们私下里都一起聊过些什么？
泰勒：无非就是些男模女模的事呀
泰勒：当然也交流过一些关于怎样更好地寻找那方面客户的话题
泰勒：但是从来没有聊过仿生人组织这些啊
泰勒：警官你一定相信我啊
->branch_E_4

= branch_E_3
*[你指使他勾引张彤]
我：你指使他勾引过城市机械银行的代理行长张彤
我：是不是？
泰勒：是，警官，是，这些问题我会交待的
泰勒：但是我真的不知道他和仿生人组织的勾当
泰勒：我也没有参与过啊，警官，这是千真万确的
->branch_E_4

= branch_E_4
*[最后一次联络]
我：你最后一次和他联系是什么时候？
泰勒：大概是今年初吧
泰勒：因为那时我想让他和一起去出席一下与一些经纪人的聚会
泰勒：但是他说不想再跟着我干了
泰勒：我也就作罢了
-> check_progress

=== check_progress ===
{ questions_asked < 3:
{&我：我们来聊聊下一个问题|我：我们换个话题|我：我们再聊点别的}
-> question_hub
-else:
我：我们就先聊到这
->END
}